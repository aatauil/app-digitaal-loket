PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX lblodOrg: <http://lblod.data.gift/vocabularies/organisatie/>
PREFIX ere: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX regorg: <http://www.w3.org/ns/regorg#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX schema: <http://schema.org/>
PREFIX locn: <http://www.w3.org/ns/locn>

DELETE {
  GRAPH ?g {
    ?position ?pposition ?oposition .
  }
}
WHERE {
  GRAPH ?g {
    ?worshipAgentInPosition a <http://data.lblod.info/vocabularies/erediensten/RolBedienaar> ;
      org:holds ?position .

    ?position ?pposition ?oposition .
  }
}

;

DELETE {
  GRAPH ?g {
    ?contactPoint ?pcontactPoint ?ocontactPoint .
    ?address ?paddress ?oaddress .
  }
}
WHERE {
  GRAPH ?g {
    ?worshipAgentInPosition a <http://data.lblod.info/vocabularies/erediensten/RolBedienaar> ;
      schema:contactPoint ?contactPoint .

    ?contactPoint ?pcontactPoint ?ocontactPoint .

    OPTIONAL {
      ?contactPoint locn:address ?address .
      ?address ?paddress ?oaddress .
    }
  }
}

;

DELETE {
  GRAPH ?g {
    ?financiering ?pfinanciering ?ofinanciering .
  }
}
WHERE {
  GRAPH ?g {
    ?worshipAgentInPosition a <http://data.lblod.info/vocabularies/erediensten/RolBedienaar> .

    OPTIONAL {
      ?worshipAgentInPosition ere:financiering ?financiering .
      ?financiering ?pfinanciering ?ofinanciering .
    }
  }
}

;

DELETE {
  GRAPH ?g {
    ?person ?pperson ?operson .
  }
}
WHERE {
  GRAPH ?g {
    ?worshipAgentInPosition a <http://data.lblod.info/vocabularies/erediensten/RolBedienaar> .

    OPTIONAL {
      ?worshipAgentInPosition org:heldBy ?person .
      ?person ?pperson ?operson .
    }
  }
}

;

DELETE {
  GRAPH ?g {
    ?worshipAgentInPosition ?pworshipAgentInPosition ?oworshipAgentInPosition .
  }
}
WHERE {
  GRAPH ?g {
    ?worshipAgentInPosition a <http://data.lblod.info/vocabularies/erediensten/RolBedienaar> ;
      ?pworshipAgentInPosition ?oworshipAgentInPosition .
  }
}
