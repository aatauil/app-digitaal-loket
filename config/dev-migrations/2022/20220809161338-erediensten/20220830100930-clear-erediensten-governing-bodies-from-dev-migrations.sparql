PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX lblodOrg: <http://lblod.data.gift/vocabularies/organisatie/>
PREFIX ere: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX regorg: <http://www.w3.org/ns/regorg#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX schema: <http://schema.org/>
PREFIX locn: <http://www.w3.org/ns/locn>

DELETE {
  GRAPH ?g {
    ?bestuursorgaan ?pbestuursorgaan ?obestuursorgaan .
    ?bestuursorgaanInTime ?pbestuursorgaanInTime ?obestuursorgaanInTime .
  }
}
WHERE {
  GRAPH ?g {
    VALUES ?type {
      ere:BestuurVanDeEredienst
      ere:CentraalBestuurVanDeEredienst
    }
  
    ?bestuurseenheid a ?type .
  
    ?bestuursorgaan a besluit:Bestuursorgaan ;
      besluit:bestuurt ?bestuurseenheid ;
      ?pbestuursorgaan ?obestuursorgaan .
  
    ?bestuursorgaanInTime a besluit:Bestuursorgaan ;
      generiek:isTijdspecialisatieVan ?bestuursorgaan ;
      ?pbestuursorgaanInTime ?obestuursorgaanInTime .
  }
}
