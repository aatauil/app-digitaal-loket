PREFIX org: <http://www.w3.org/ns/org#>
PREFIX schema: <http://schema.org/>
PREFIX ere: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX locn: <http://www.w3.org/ns/locn#>

INSERT {
  GRAPH ?orgGraph {
    ?contactPoint ?pcontactPoint ?ocontactPoint .
    ?address ?paddress ?oaddress .
  }
}
WHERE {
    ?worshipAgentInPosition a <http://data.lblod.info/vocabularies/erediensten/RolBedienaar> ;
      org:holds ?position ;
      schema:contactPoint ?contactPoint .

    ?contactPoint ?pcontactPoint ?ocontactPoint .

    OPTIONAL {
      ?contactPoint locn:address ?address .
      ?address ?paddress ?oaddress .
    }

    ?eenheid ere:wordtBediendDoor ?position ;
      <http://mu.semte.ch/vocabularies/core/uuid> ?uuid .

    BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", ?uuid)) AS ?orgGraph)
}

