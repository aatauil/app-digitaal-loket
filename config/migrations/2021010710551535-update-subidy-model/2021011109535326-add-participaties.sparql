PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX pav: <http://purl.org/pav/>

INSERT {
  GRAPH ?g {
    ?bestuurseenheid m8g:playsRole ?participatie .
    ?consumptie m8g:hasParticipation ?participatie .
    ?participatie a m8g:Participation ;
      mu:uuid ?uuidParticipatie .
  }
}
WHERE {
  GRAPH ?g {
    ?consumptie a subsidie:SubsidiemaatregelConsumptie ;
      prov:wasGeneratedBy ?aanvraag .
    ?aanvraag prov:used ?applicationForm .
    ?applicationForm pav:createdBy ?bestuurseenheid .

    BIND(MD5(CONCAT(?applicationForm, "SubsidiemaatregelConsumptie")) as ?uuidParticipatie)
    BIND(IRI(CONCAT("http://data.lblod.info/id/participaties/", ?uuidParticipatie)) AS ?participatie)
  }
}
