PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX lpdcExt: <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#>

# This query assumes the add-concept-display-configurations query has added configs to all concepts for every bestuurseenheid already

DELETE {
  GRAPH ?graph {
    ?config <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#conceptInstantiated> ?instantiated.
  }
}
INSERT {
  GRAPH ?graph {
    ?config <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#conceptInstantiated> "true"^^<http://mu.semte.ch/vocabularies/typed-literals/boolean>.
  }
}
WHERE {

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?concept a lpdcExt:ConceptualPublicService .
  }

  GRAPH ?graph {
    ?instance a cpsv:PublicService ;
      dct:source ?concept .
    ?concept lpdcExt:hasConceptDisplayConfiguration ?config.

    ?config a <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#ConceptDisplayConfiguration>;
      <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#conceptInstantiated> ?instantiated.
  }
}