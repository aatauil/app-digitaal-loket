PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX lpdcExt: <https://productencatalogus.data.vlaanderen.be/ns/ipdc-lpdc#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX pav: <http://purl.org/pav/>

INSERT {
  GRAPH ?graph {
    ?concept lpdcExt:hasConceptDisplayConfiguration ?configUri .
    ?configUri a lpdcExt:ConceptDisplayConfiguration ;
      mu:uuid ?configId ;
      lpdcExt:conceptIsNew false ;
      lpdcExt:conceptInstantiated false ;
      dct:relation ?eenheid .
  }
}
WHERE {
  VALUES ?eenheidClassificatie {
    <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000000> # provincie
    <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000001> # gemeente
  }

  ?eenheid a besluit:Bestuurseenheid ;
    mu:uuid ?eenheidId ;
    besluit:classificatie ?eenheidClassificatie .

  BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", STR(?eenheidId), "/LoketLB-LPDCGebruiker")) as ?graph)

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?concept a lpdcExt:ConceptualPublicService
  }

  # this is a bit of trickery to generate UUID and URI's since STRUUID doesn't work properly in Virtuoso: https://github.com/openlink/virtuoso-opensource/issues/515#issuecomment-456848368
  BIND(SHA512(CONCAT(STR(?concept), STR(?eenheidId))) as ?configId) # concept + eenheid should be unique per config object
  BIND(IRI(CONCAT('http://data.lblod.info/id/conceptual-display-configuration/', STR(?configId))) as ?configUri)
}