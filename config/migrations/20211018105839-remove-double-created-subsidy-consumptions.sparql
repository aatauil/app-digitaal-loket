PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX schema: <http://schema.org/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX bicycleInfrastructure: <http://lblod.data.gift/vocabularies/subsidie/bicycle-infrastructure#>
PREFIX transactie: <http://data.vlaanderen.be/ns/transactie#>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX common: <http://www.w3.org/2007/uwa/context/common.owl#>

# Remove participation
DELETE {
    GRAPH ?g {
        ?participation m8g:role ?role .
        ?org m8g:playsRole ?participation .
    }
} WHERE {
    VALUES ?consumptionId {
        "6167F46BE5754600090000C8"
        "616826BDE57546000900018F"
        "61681ED2E575460009000147"
    }
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            m8g:hasParticipation ?participation .
        ?participation m8g:role ?role .
        ?org m8g:playsRole ?participation .
    }
}
;
# Remove form-data
DELETE {
    GRAPH ?g {
        ?form dct:created ?created ;
            dct:modified ?modified ;
            dct:isPartOf ?isPartOf ;
            adms:status ?status ;
            lblodSubsidie:projectName ?projectName ;
            nie:identifier ?identifier ;
            lblodSubsidie:decisionUpload ?decisionUploadNode ;
            lblodSubsidie:picturesUpload ?picturesUploadNode ;
            schema:contactPoint ?contactPoint ;
            bicycleInfrastructure:objectiveTable ?objectiveTable ;
            lblodSubsidie:totalEstimateCost ?totalExtimateCost .

        ?decisionUploadNode dct:hasPart ?decisionUploadFile .
        ?picturesUploadNode dct:hasPart ?picturesUploadFile .

        # Contact Info
        ?contactPoint foaf:firstName ?firstName ;
            foaf:familyName ?familyName ;
            schema:telephone ?telephone ;
            schema:email ?email .

        # Estimated Cost Table
        ?estimatedCostTable bicycleInfrastructure:validEstimatedCostTable ?validEstimatedCostTable ;
            bicycleInfrastructure:estimatedCostEntry ?estimatedCostEntry .
        ?estimatedCostEntry bicycleInfrastructure:costEstimationType ?costEstimationType ;
            bicycleInfrastructure:cost ?cost ;
            bicycleInfrastructure:share ?share ;
            ext:index ?index .

        # Objective Table
        ?objectiveTable bicycleInfrastructure:validObjectiveTable ?validObjectiveTable ;
            bicycleInfrastructure:objectiveEntry ?objectiveEntry .
        ?objectiveEntry bicycleInfrastructure:approachType ?approachType ;
            bicycleInfrastructure:directionType ?directionType ;
            bicycleInfrastructure:bikeLaneType ?bikeLaneType ;
            bicycleInfrastructure:kilometers ?kilometers .
    }
} WHERE {
    VALUES ?consumptionId {
        "6167F46BE5754600090000C8"
        "616826BDE57546000900018F"
        "61681ED2E575460009000147"
    }
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            dct:source ?form .
        ?form dct:created ?created ;
            dct:modified ?modified ;
            dct:isPartOf ?isPartOf ;
            adms:status ?status ;
            lblodSubsidie:projectName ?projectName ;
            nie:identifier ?identifier ;
            lblodSubsidie:decisionUpload ?decisionUploadNode ;
            lblodSubsidie:picturesUpload ?picturesUploadNode ;
            schema:contactPoint ?contactPoint ;
            bicycleInfrastructure:objectiveTable ?objectiveTable .

        OPTIONAL { ?form lblodSubsidie:totalEstimateCost ?totalExtimateCost .}

        ?decisionUploadNode dct:hasPart ?decisionUploadFile .
        ?picturesUploadNode dct:hasPart ?picturesUploadFile .

        # Contact Info
        ?contactPoint foaf:firstName ?firstName ;
            foaf:familyName ?familyName ;
            schema:telephone ?telephone ;
            schema:email ?email .

        # Estimated Cost Table
        ?estimatedCostTable bicycleInfrastructure:validEstimatedCostTable ?validEstimatedCostTable ;
            bicycleInfrastructure:estimatedCostEntry ?estimatedCostEntry .
        ?estimatedCostEntry bicycleInfrastructure:costEstimationType ?costEstimationType ;
            bicycleInfrastructure:cost ?cost ;
            bicycleInfrastructure:share ?share ;
            ext:index ?index .

        # Objective Table
        ?objectiveTable bicycleInfrastructure:validObjectiveTable ?validObjectiveTable ;
            bicycleInfrastructure:objectiveEntry ?objectiveEntry .
        ?objectiveEntry bicycleInfrastructure:approachType ?approachType ;
            bicycleInfrastructure:directionType ?directionType ;
            bicycleInfrastructure:bikeLaneType ?bikeLaneType ;
            bicycleInfrastructure:kilometers ?kilometers .
    }
}
;
# Remove consumption
DELETE {
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            dct:created ?consumptionCreated ;
            dct:modified ?consumptionModified ;
            transactie:isInstantieVan ?subsidyMeasureOffer ;
            cpsv:follows ?flow ;
            common:active ?currentStep ;
            adms:status ?status ;
            ext:lastModifiedBy ?lastModifier ;
            dct:creator ?creator ;
            m8g:hasParticipation ?participation ;
            dct:source ?form .
    }
} WHERE {
    VALUES ?consumptionId {
        "6167F46BE5754600090000C8"
        "616826BDE57546000900018F"
        "61681ED2E575460009000147"
    }
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            dct:created ?consumptionCreated ;
            dct:modified ?consumptionModified ;
            transactie:isInstantieVan ?subsidyMeasureOffer ;
            cpsv:follows ?flow ;
            common:active ?currentStep ;
            adms:status ?status ;
            ext:lastModifiedBy ?lastModifier ;
            dct:creator ?creator ;
            m8g:hasParticipation ?participation ;
            dct:source ?form .
    }
}